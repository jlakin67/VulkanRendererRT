#version 460 core
#extension GL_EXT_mesh_shader : require

//layout( local_size_x_id=0 ) in;

//one workgroup per meshlet, per instance
//each thread operates on one vertex and outputs one primitive (triangle) in the meshlet

const uint MAX_MESHLET_VERTICES = 32;
const uint MAX_MESHLET_PRIMITIVES = 32;
const uint MAX_MESHLET_INDICES = MAX_MESHLET_PRIMITIVES * 3;

layout( local_size_x = 32, local_size_y = 1, local_size_z = 1 ) in;
layout(triangles, max_vertices = MAX_MESHLET_VERTICES, max_primitives = MAX_MESHLET_PRIMITIVES) out;

layout (std140, set = 0, binding = 0) uniform Transforms { 
  mat4 view;
  mat4 projection;
} transforms;

layout (std140, set = 1, binding = 0) readonly buffer Instances { 
  mat4 models[];
} instances;

struct Vertex {
  vec4 position;
};

layout (std430, set = 1, binding = 1) readonly buffer VertexBuffer {
  Vertex vertices[];
} vertexBuffer;

struct Meshlet
{
  uint vertices[MAX_MESHLET_VERTICES];
  uint indices[MAX_MESHLET_INDICES];
  uint vertexCount;
  uint indexCount;
};

layout (std430, set = 1, binding = 2) readonly buffer MeshletBuffer
{
  Meshlet meshlets[];
} meshletBuffer;

layout (location = 0) out PerVertexData
{
  //vec3 pos;
  flat vec3 color;
} v_out[];   // [max_vertices]

const vec3 colors[7] = {vec3(1.0f,0.0f,0.0f), vec3(1.0f,0.5f,0.0f), vec3(1.0f, 1.0f, 0.0f), vec3(0.0f, 1.0f, 0.0f),
	vec3(0.0f, 0.0f, 1.0f), vec3(0.5f, 0.0f, 1.0f), vec3(1.0f, 0.0f, 0.5f)};

void main() {
	uint instanceID = gl_WorkGroupID.y; //the y axis used for instances
	mat4 model = instances.models[instanceID];
	uint meshletID = gl_WorkGroupID.x;
	Meshlet meshlet = meshletBuffer.meshlets[meshletID];
	uint vertexID = gl_LocalInvocationID.x;
	uint primitiveID = gl_LocalInvocationID.x;
	uint primitiveCount = meshlet.indexCount / 3;
	SetMeshOutputsEXT(meshlet.vertexCount, primitiveCount);
	if (vertexID < meshlet.vertexCount) {
		uint vertexIndex = meshlet.vertices[vertexID];
		vec4 worldPos = model * vertexBuffer.vertices[vertexIndex].position;
		//v_out[vertexID].pos = worldPos.xyz;
		v_out[vertexID].color = colors[meshletID % 7];
		gl_MeshVerticesEXT[vertexID].gl_Position = transforms.projection * transforms.view * worldPos;
	}
	if (primitiveID < primitiveCount) {
		uvec3 triangleIndices = uvec3(meshlet.indices[3*primitiveID], meshlet.indices[3*primitiveID+1], meshlet.indices[3*primitiveID+2]);
		gl_PrimitiveTriangleIndicesEXT[primitiveID] = triangleIndices;
	}
}

